program = globalDeclaration* EOF ;


# --------------- Declarations --------------- 

globalDeclaration = recordDecl 
                  | classDecl
                  | subDecl
                  | declaration ;

recordDecl        = "RECORD" IDENTIFER "IS" "{" parameters "}" SEPARATOR? ;

classDecl         = "CLASS" IDENTIFER ( ( "INHERITS" IDENTIFER ( "WITH" "{" parameters "}" )? ) | ( "IS" "{" parameters "}" ) )? SEPARATOR?
                    ( "METHODS" SEPARATOR? ( classConstructor | "OVERRIDE"? subDecl )* )?
                    "END" "CLASS" SEPARATOR? ;
classConstructor  = "CONSTRUCTOR" "(" parameters? ")" body "END" "CONSTRUCTOR" SEPARATOR? ;

subDecl           = procDecl | funcDecl ;
procDecl          = "PROCEDURE" procedure "END" "PROCEDURE" SEPARATOR? ;
funcDecl          = "FUNCTION" function "END" "FUNCTION" SEPARATOR? ;

declaration       = varDecl | statement ;
varDecl           = "DECLARE" IDENTIFER ( "AS" TYPE_DECL )? "INITIALLY" ( ( "FROM" SYS_ENTITY ) | expression ) SEPARATOR ;


# --------------- Statements --------------- 

statement   = ifStmt
            | whileStmt | untilStmt
            | forStmt | forEachStmt 
            | setStmt 
            | createStmt | openStmt | closeStmt
            | sendStmt | recieveStmt 
            | returnStmt
            | exprStmt ;

ifStmt      = "IF" expression "THEN" body ( "ELSE" body )? "END" "IF" SEPARATOR? ;

whileStmt   = "WHILE" expression "DO" body "END" "WHILE" SEPARATOR? ;
untilStmt   = "REPEAT" body "UNTIL" expression SEPARATOR ;

forStmt     = "FOR" IDENTIFER "FROM" expression "TO" expression ( "STEP" expression )? "DO" body "END" "FOR" SEPARATOR? ;
forEachStmt = "FOR" "EACH" IDENTIFER "FROM" expression "DO" body "END" "FOR" "EACH" SEPARATOR? ;

setStmt     = "SET" expression "TO" expression SEPARATOR ;

createStmt  = "CREATE" FILE SEPARATOR ;
openStmt    = "OPEN" FILE SEPARATOR ;
closeStmt   = "CLOSE" FILE SEPARATOR ;

sendStmt    = "SEND" expression "TO" SYS_ENTITY SEPARATOR ;
recieveStmt = "RECIEVE" expression "FROM" SYS_ENTITY SEPARATOR ;

returnStmt  = "RETURN" expression? SEPARATOR;

exprStmt    = expression SEPARATOR ;


# --------------- Expressions --------------- 
expression    = logic_or ;

logic_or      = logic_and ( "OR" logic_and )* ;
logic_and     = logic_not ( "AND" logic_not )* ;
logic_not     = ( "NOT" ) logic_not | equality ;
equality      = comparison ( ( "≠" | "=" ) comparison )* ;
comparison    = concatenation ( ( ">" | "≥" | "<" | "≤" ) concatenation )* ;
concatenation = term ( ( "&" ) term )* ;
term          = factor ( ( "-" | "+" ) factor )* ;
factor        = exponent ( ( "/" | "*" | "MOD" ) exponent )* ;
exponent      = unary ( ( "^" ) unary )* ;

unary         = ( "-" ) unary | call ;
call          = primary ( "(" arguments? ")" | "." IDENTIFIER | "[" expression "]" )* ;
primary       = TYPE | IDENTIFIER
              | "(" expression ")"
              | "THIS" ;


# --------------- Types --------------- 
TYPE       = INTEGER | REAL | BOOLEAN | CHARACTER | STRING | ARRAY | RECORD ;

INTEGER    = DIGIT+ ; 
REAL       = DIGIT+ ( "." DIGIT+ ) ;
BOOLEAN    = "true" | "false" ;
CHARACTER  = "'" <any char except "'"> "'" ;
STRING     = "\"" <any char except "\"">* "\"" ;
ARRAY      = "[" arguments? "]" ( "*" INTEGER )? ;
RECORD     = "{" IDENTIFER "=" expression ( "," IDENTIFER "=" expression)* "}" ; # RECORD expression

IDENTIFER  = ALPHA ( ALPHA | DIGIT )* ; 
SYS_ENTITY = "DISPLAY" | "KEYBOARD" | FILE ; 
FILE       = <file path or URL> ;

ALPHA      = "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT      = "0" ... "9" ;


# --------------- Type Declarations --------------- 
TYPE_DECL    = INTEGER_TYPE | REAL_TYPE | CHARACTER_TYPE | STRING_TYPE | ARRAY_TYPE | IDENTIFER ;

INTEGER_TYPE = "INTEGER" ;
REAL_TYPE    = "REAL" ;
STRING_TYPE  = "CHARACTER" ;
STRING_TYPE  = "STRING" ;
ARRAY_TYPE   = "ARRAY OF" TYPE_DECL ;


# --------------- Utility Rules --------------- 
procedure  = IDENTIFER "(" parameters? ")" body ;
function   = IDENTIFER "(" parameters? ")" "RETURNS" TYPE_DECL body ;
body       = SEPARATOR? declaration* ;

parameters = SEPARATOR? TYPE_DECL IDENTIFIER ( "," SEPARATOR? TYPE_DECL IDENTIFIER )* SEPARATOR? ;
arguments  = SEPARATOR? expression ( "," SEPARATOR? expression)* SEPARATOR? ;

SEPARATOR  = ( "\n" ) ;